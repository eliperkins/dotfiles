# to profile zsh launch time, uncomment this and `zprof` at the bottom
# zmodload zsh/zprof

if [[ $CODESPACES == "true" ]];
then
  export ZSH=/workspaces/.codespaces/.persistedshare/dotfiles
  export PROJECTS=/workspaces
else
  # shortcut to this dotfiles path is $ZSH
  export ZSH=$HOME/.dotfiles

  # your project folder that we can `c [tab]` to
  export PROJECTS=~/src
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  # echo "(zshrc) loading $file"
  source $file
done

fpath+=~/.zfunc

autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done
compinit -C

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

autoload -Uz promptinit
promptinit

# zprof

export PATH="$PATH:$HOME/.local/bin"
