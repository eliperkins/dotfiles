# to profile zsh launch time, uncomment this and `zprof` at the bottom
# zmodload zsh/zprof

if [[ $CODESPACES == "true" ]]; 
then
  export ZSH=/workspaces/.codespaces/.persistedshare/dotfiles
  export PROJECTS=/workspaces
else
  # shortcut to this dotfiles path is $ZSH
  export ZSH=$HOME/.dotfiles

  # your project folder that we can `c [tab]` to
  export PROJECTS=~/src
fi

# Homebrew flags
export HOMEBREW_USE_CLANG

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

fpath+=~/.zfunc

autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+12); do
  compinit
done
compinit -C

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

if test "$(uname -s)" = "Darwin"; then
  source $(brew --prefix)/etc/profile.d/z.sh
fi

autoload -Uz promptinit
promptinit

# damn you random tools that want to append yourself to my $PATH
export PATH="$HOME/.yarn/bin:$PATH"
export PATH="$HOME/src/github/awssume/bin:$PATH"
export PATH="/usr/local/opt/bison/bin:$PATH"
export PATH="/usr/local/opt/gettext/bin:$PATH"

# zprof
